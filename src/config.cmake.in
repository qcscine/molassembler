include(CMakeFindDependencyMacro)

# Public interface type dependencies
find_dependency(ScineUtilsOS REQUIRED)

if(NOT DEFINED Boost_VERSION)
  find_package(Boost QUIET)
endif()

if(NOT TARGET Boost::filesystem OR NOT TARGET Boost::system)
  unset(Boost_FOUND)
  find_dependency(Boost REQUIRED COMPONENTS filesystem system)
endif()

set(BOOST_HAS_OUTCOME 0)
if(${Boost_VERSION} MATCHES "\\.")
  if(${Boost_VERSION} VERSION_GREATER_EQUAL 1.70.0)
    set(BOOST_HAS_OUTCOME 1)
  endif()
else()
  if(${Boost_VERSION} GREATER_EQUAL 107000)
    set(BOOST_HAS_OUTCOME 1)
  endif()
endif()

if(BOOST_HAS_OUTCOME)
  find_dependency(Outcome REQUIRED)
endif()

# Only in a static build of molassembler do we need to be able to link these
# subsumed static libraries again
if(NOT @BUILD_SHARED_LIBS@)
  find_dependency(RingDecomposerLib REQUIRED)
  find_dependency(nauty REQUIRED)
endif()

if(NOT "${SCINE_MARCH}" STREQUAL "@SCINE_MARCH@")
  message(FATAL_ERROR "You are trying to use an installed version of molassembler that was compiled with different -march flag values! Current march flags: ${SCINE_MARCH}, installed flags: @SCINE_MARCH@")
endif()

set(MOLASSEMBLER_VERSION @Molassembler_VERSION@)

include(${CMAKE_CURRENT_LIST_DIR}/ScineMolassemblerTargets.cmake)
